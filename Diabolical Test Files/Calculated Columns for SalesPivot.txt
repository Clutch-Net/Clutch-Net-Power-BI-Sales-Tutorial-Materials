// Calculated Columns for Diabolical Test
// Clutch.Net tutorial and demo - Tom Sampson © Clutch.Net 2023
// free to use under the MIT License. See project LICENSE for more details.

// RankCustDate
// Clutch.Net tutorial and demo - Tom Sampson © Clutch.Net 2023
// free to use under the MIT License. See project LICENSE for more details.
RankCustDate = 
RANKX(
    FILTER(
        'SalesPivot',
        'SalesPivot'[CustomerKey] = EARLIER('SalesPivot'[CustomerKey])
    ),
    'SalesPivot'[OrderDate],
    ,
    ASC,
    Dense
)

// OrderDateAfterFirstDate
// Clutch.Net tutorial and demo - Tom Sampson © Clutch.Net 2023
// free to use under the MIT License. See project LICENSE for more details.
OrderDateAfterFirstDate = 
VAR CurrentCustomerKey = SalesPivot[CustomerKey]
VAR FirstOrderDate = CALCULATE(
    MIN(SalesPivot[OrderDate]),
    FILTER(
        SalesPivot, 
        SalesPivot[CustomerKey] = CurrentCustomerKey && SalesPivot[RankCustDate] = 1
    )
)
VAR OrderAfterFirstDate = 
    CALCULATE(
        MIN(SalesPivot[OrderDate]), 
        FILTER(
            SalesPivot, 
            SalesPivot[CustomerKey] = CurrentCustomerKey &&
            SalesPivot[OrderDate] <> FirstOrderDate
        )
    )
RETURN 
IF(
    SalesPivot[RankCustDate] <> 1, 
    BLANK(),
    IF(
        ISBLANK(OrderAfterFirstDate),
        BLANK(),
        OrderAfterFirstDate
    )
)

// OrderDateAfterFirstMonth
// Clutch.Net tutorial and demo - Tom Sampson © Clutch.Net 2023
// free to use under the MIT License. See project LICENSE for more details.
OrderDateAfterFirstMonth = 
VAR CurrentCustomerKey = SalesPivot[CustomerKey]
VAR FirstOrderDate = CALCULATE(
    MIN(SalesPivot[OrderDate]),
    FILTER(
        SalesPivot, 
        SalesPivot[CustomerKey] = CurrentCustomerKey && SalesPivot[RankCustDate] = 1
    )
)
VAR FirstOrderMonth = MONTH(FirstOrderDate)
VAR FirstOrderYear = YEAR(FirstOrderDate)
VAR OrderAfterFirstMonth = 
    CALCULATE(
        MIN(SalesPivot[OrderDate]), 
        FILTER(
            SalesPivot, 
            SalesPivot[CustomerKey] = CurrentCustomerKey &&
            (MONTH(SalesPivot[OrderDate]) <> FirstOrderMonth || YEAR(SalesPivot[OrderDate]) <> FirstOrderYear) 
        )
    )
RETURN 
IF(
    SalesPivot[RankCustDate] <> 1, 
    BLANK(),
    IF(
        ISBLANK(OrderAfterFirstMonth),
        BLANK(),
        OrderAfterFirstMonth
    )
)

// FlagWithin90Days
// Clutch.Net tutorial and demo - Tom Sampson © Clutch.Net 2023
// free to use under the MIT License. See project LICENSE for more details.
FlagWithin90Days = 
VAR DaysBetweenFirstAndSecond = SalesPivot[OrderDateAfterFirstDate] - SalesPivot[OrderDate]

RETURN
IF(
    NOT(ISBLANK(SalesPivot[OrderDateAfterFirstDate])) &&
    NOT(ISBLANK(SalesPivot[OrderDate])) &&
    SalesPivot[RankCustDate] = 1 &&
    DaysBetweenFirstAndSecond > 0 && 
    DaysBetweenFirstAndSecond <= 90,
    1,
    BLANK()
)

// FlagWithin3Months
// Clutch.Net tutorial and demo - Tom Sampson © Clutch.Net 2023
// free to use under the MIT License. See project LICENSE for more details.
FlagWithin3Months = 
IF(
    NOT(ISBLANK('SalesPivot'[OrderDate])) &&
    NOT(ISBLANK('SalesPivot'[OrderDateAfterFirstMonth])) &&
    (
        (
            YEAR('SalesPivot'[OrderDateAfterFirstMonth]) = YEAR('SalesPivot'[OrderDate]) && 
            MONTH('SalesPivot'[OrderDateAfterFirstMonth]) > MONTH('SalesPivot'[OrderDate]) && 
            MONTH('SalesPivot'[OrderDateAfterFirstMonth]) <= MONTH('SalesPivot'[OrderDate]) + 3
        )
        ||
        (
            MONTH('SalesPivot'[OrderDate]) > 9 && 
            YEAR('SalesPivot'[OrderDateAfterFirstMonth]) = YEAR('SalesPivot'[OrderDate]) + 1 && 
            MONTH('SalesPivot'[OrderDateAfterFirstMonth]) <= (MONTH('SalesPivot'[OrderDate]) + 3) - 12
        )
    ),
    1,
    BLANK()
)





